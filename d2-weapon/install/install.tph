//
//


//check for missing game resources
ACTION_BASH_FOR ~%resource%~  ~.*~ BEGIN
  ACTION_IF !(FILE_EXISTS_IN_GAME ~%BASH_FOR_FILE%~) BEGIN    // check for missing files
    COPY ~%BASH_FOR_FILESPEC%~  ~override~
  END
END



//Ranged

//Black Bow of Pharis (d2bow01)
OUTER_TEXT_SPRINT itemid  ~d2bow01~
INCLUDE ~%install%/variables.tph~
COPY ~%icons%/blackbowi.BAM~  ~override/%ii%.BAM~
COPY ~%items%/blackbow.ITM~   ~override/%itemid%.ITM~
  SAY NAME1 @100
  SAY NAME2 @100 SAY DESC @200
  WRITE_ASCII 0x3a ~%ii%~ #8      // icon
  WRITE_ASCII 0x58 ~CBOW03~ #8    // description image
  LPF ALTER_ITEM_HEADER STR_VAR icon=~%ii%~ END


//Dragonslayer Greatbow (d2bow02)
OUTER_TEXT_SPRINT itemid  ~d2bow02~
INCLUDE ~%install%/variables.tph~
COPY ~%icons%/greatbowi.BAM~  ~override/%ii%.BAM~
COPY ~%items%/greatbow.ITM~   ~override/%itemid%.ITM~
  SAY NAME1 @101
  SAY NAME2 @101 SAY DESC @201
  WRITE_ASCII 0x3a ~%ii%~ #8      // icon
  WRITE_ASCII 0x58 ~CBOW12~ #8    // description image
  LPF ALTER_ITEM_HEADER STR_VAR icon=~%ii%~ END


//White Birch Bow (d2bow03)
OUTER_TEXT_SPRINT itemid  ~d2bow03~
INCLUDE ~%install%/variables.tph~
COPY ~%icons%/whitebirchi.BAM~        ~override/%ii%.BAM~
COPY ~%items%/whitebirch.ITM~         ~override/%itemid%.ITM~
  SAY NAME1 @102
  SAY NAME2 @102 SAY DESC @202
  WRITE_ASCII 0x3a ~%ii%~ #8      // icon
  WRITE_ASCII 0x58 ~CBOW15~ #8    // description image
  LPF ALTER_ITEM_HEADER STR_VAR icon=~%ii%~ END

COPY ~%effects%/castspelltarget.EFF~  ~override/%ee%.EFF~
  WRITE_ASCII 0x30 ~%ee%~
COPY ~%effects%/whitebirche.SPL~      ~override/%ee%.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@300) END    // "Invisible"

COPY ~%effects%/castspelltarget.EFF~  ~override/%ss%.EFF~
  WRITE_ASCII 0x30 ~%ss%~
COPY ~%effects%/whitebirchs.SPL~      ~override/%ss%.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@300) END    // "Invisible"


//Avelyn (d2bow04)
OUTER_TEXT_SPRINT itemid  ~d2bow04~
INCLUDE ~%install%/variables.tph~
COPY ~%icons%/avelyni.BAM~    ~override/%ii%.BAM~
COPY ~%items%/avelyn.ITM~     ~override/%itemid%.ITM~
  SAY NAME1 @103
  SAY NAME2 @103 SAY DESC @203
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@301) END    // "Repeat Fire"
  WRITE_ASCII 0x3a ~%ii%~ #8       // icon
  WRITE_ASCII 0x58 ~CXBOW13~ #8    // description image
  LPF ALTER_ITEM_HEADER STR_VAR icon=~%ii%~ END

COPY ~%effects%/avelyne.ITM~  ~override/%ee%.ITM~
  SAY NAME1 @103
  SAY NAME2 @103 SAY DESC @203
  WRITE_ASCII 0x3a ~%ii%~ #8       // icon
  WRITE_ASCII 0x58 ~CXBOW13~ #8    // description image
  LPF ALTER_ITEM_HEADER STR_VAR icon=~%ii%~ END

LAF d2_elemental_weapon_arrays STR_VAR item=~%itemid%~ element=~electrical~ RET_ARRAY d2electricalweap END
LAF d2_elemental_weapon_arrays INT_VAR temp=1 STR_VAR item=~%ee%~ element=~electrical~ RET_ARRAY d2electricalweap END




//2-handed weapons

//Greatsword (d2swd01)
OUTER_TEXT_SPRINT itemid  ~d2swd01~
INCLUDE ~%install%/variables.tph~
COPY ~%icons%/greatswordi.BAM~  ~override/%ii%.BAM~
COPY ~%items%/greatsword.ITM~   ~override/%itemid%.ITM~
  SAY NAME1 @104
  SAY NAME2 @104 SAY DESC @204
  WRITE_ASCII 0x3a ~%ii%~ #8       // icon
  WRITE_ASCII 0x58 ~CSW2H09~ #8    // description image
  LPF ALTER_ITEM_HEADER STR_VAR icon=~%ii%~ END


//Fume Ultra Greatsword (d2swd02)
OUTER_TEXT_SPRINT itemid  ~d2swd02~
INCLUDE ~%install%/variables.tph~
COPY ~%icons%/fumeultraGSi.BAM~  ~override/%ii%.BAM~
COPY ~%items%/fumeultraGS.ITM~   ~override/%itemid%.ITM~
  SAY NAME1 @105
  SAY NAME2 @105 SAY DESC @205
  WRITE_ASCII 0x3a ~%ii%~ #8       // icon
  WRITE_ASCII 0x58 ~CSW2H08~ #8    // description image
  LPF ALTER_ITEM_HEADER STR_VAR icon=~%ii%~ END


//Stone Greatsword (d2swd06)
OUTER_TEXT_SPRINT itemid  ~d2swd06~
INCLUDE ~%install%/variables.tph~
COPY ~%icons%/stoneGSi.BAM~           ~override/%ii%.BAM~
COPY ~%items%/stoneGS.ITM~            ~override/%itemid%.ITM~
  SAY NAME1 @106
  SAY NAME2 @106 SAY DESC @206
  WRITE_ASCII 0x3a ~%ii%~ #8       // icon
  WRITE_ASCII 0x58 ~CSW1H34~ #8    // description image
  LPF ALTER_ITEM_HEADER STR_VAR icon=~%ii%~ END

COPY ~%effects%/stoneGSs.SPL~         ~override/%ss%.SPL~
  SAY NAME1 @302
COPY ~%effects%/stoneGSe.ITM~         ~override/%ee%.ITM~
  SAY NAME1 @106
  SAY NAME2 @106 SAY DESC @206
  WRITE_ASCII 0x3a ~%ii%~ #8       // icon
  WRITE_ASCII 0x58 ~CSW1H34~ #8    // description image
  LPF ALTER_ITEM_HEADER STR_VAR icon=~%ii%~ END

COPY - ~%effects%/stoneGSe.PRO~       ~%workspace%/%ee%.PRO~
ADD_PROJECTILE ~%workspace%/%ee%.PRO~
COPY ~%effects%/castspelltarget.EFF~  ~override/%ee%.EFF~
  WRITE_ASCII 0x30 ~%ee%~
COPY ~%effects%/stoneGSe.SPL~         ~override/%ee%.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@303) END    // "Slowed"
  WRITE_SHORT 0x98 ~%ee%~    // projectile




//1-handed weapons

//Chaos Rapier (d2swd03)
OUTER_TEXT_SPRINT itemid  ~d2swd03~
INCLUDE ~%install%/variables.tph~
COPY ~%icons%/chaosrapieri.BAM~       ~override/%ii%.BAM~
COPY ~%items%/chaosrapier.ITM~        ~override/%itemid%.ITM~
  SAY NAME1 @107
  SAY NAME2 @107 SAY DESC @207
  WRITE_ASCII 0x3a ~%ii%~ #8        // icon
  WRITE_ASCII 0x58 ~BDCSWH08~ #8    // description image
  LPF ALTER_ITEM_HEADER STR_VAR icon=~%ii%~ END

/*
COPY ~%effects%/castspelltarget.EFF~  ~override/%ee%.EFF~
  WRITE_ASCII 0x30 ~%ee%~
COPY ~%effects%/chaosrapiere.SPL~     ~override/%ee%.SPL~
*/

LAF d2_elemental_weapon_arrays STR_VAR item=~%itemid%~ element=~fire~ RET_ARRAY d2fireweap END


//Drakeblood Greatsword (d2swd04)
OUTER_TEXT_SPRINT itemid  ~d2swd04~
INCLUDE ~%install%/variables.tph~
COPY ~%icons%/drakebloodi.BAM~  ~override/%ii%.BAM~
COPY ~%items%/drakeblood.ITM~   ~override/%itemid%.ITM~
  SAY NAME1 @108
  SAY NAME2 @108 SAY DESC @208
  WRITE_ASCII 0x3a ~%ii%~ #8       // icon
  WRITE_ASCII 0x58 ~CSW2H16~ #8    // description image
  LPF ALTER_ITEM_HEADER STR_VAR icon=~%ii%~ END

LAF d2_elemental_weapon_arrays STR_VAR item=~%itemid%~ element=~electrical~ RET_ARRAY d2electricalweap END


//Bewitched Alonne Sword (d2swd05)
OUTER_TEXT_SPRINT itemid  ~d2swd05~
INCLUDE ~%install%/variables.tph~
COPY ~%icons%/alonneswordi.BAM~       ~override/%ii%.BAM~
COPY ~%items%/alonnesword.ITM~        ~override/%itemid%.ITM~
  SAY NAME1 @109 SAY UNIDENTIFIED_DESC @701
  SAY NAME2 @109 SAY DESC @209
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@304) END    // "Harakiri"
  WRITE_ASCII 0x3a ~%ii%~ #8       // icon
  WRITE_ASCII 0x58 ~CSW1H44~ #8    // description image
  LPF ALTER_ITEM_HEADER STR_VAR icon=~%ii%~ END

COPY ~%icons%/bloodloss.BAM~          ~override/%ee%.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@305) STR_VAR bam=~%ee%~ RET icon END
COPY ~%effects%/castspelltarget.EFF~  ~override/%ee%.EFF~
  WRITE_ASCII 0x30 ~%ee%~
COPY ~%effects%/alonnesworde.SPL~     ~override/%ee%.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@305) END    // "Blood Loss"
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%effects%/castspelltarget.EFF~  ~override/%bb%.EFF~
  WRITE_ASCII 0x30 ~%bb%~
COPY ~%effects%/alonneswordb.SPL~     ~override/%bb%.SPL~    // Bleed damage

COPY ~%effects%/castspelltarget.EFF~  ~override/%hh%.EFF~
  WRITE_ASCII 0x30 ~%hh%~
COPY ~%effects%/alonneswordh.SPL~     ~override/%hh%.SPL~    // Harakiri self-damage

COPY ~%icons%/harakiri.BAM~           ~override/%ss%.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@304) STR_VAR bam=~%ss%~ RET icon END
COPY ~%effects%/castspelltarget.EFF~  ~override/%ss%.EFF~
  WRITE_ASCII 0x30 ~%ss%~
COPY ~%effects%/alonneswords.SPL~     ~override/%ss%.SPL~    // Harakiri buff
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END


//Aquamarine Dagger (d2dag01)
OUTER_TEXT_SPRINT itemid  ~d2dag01~
INCLUDE ~%install%/variables.tph~
COPY ~%icons%/aquamarinei.BAM~    ~override/%ii%.BAM~
COPY ~%items%/aquamarine.ITM~     ~override/%itemid%.ITM~
  SAY NAME1 @110
  SAY NAME2 @110 SAY DESC @210
  WRITE_ASCII 0x3a ~%ii%~ #8    // icon
  WRITE_ASCII 0x58 ~CDAGG16~ #8    // description image
  LPF ALTER_ITEM_HEADER STR_VAR icon=~%ii%~ END

COPY ~%effects%/aquamarinee.SPL~  ~override/%ee%.SPL~
  SAY NAME1 @306
COPY ~%effects%/aquamarinee.ITM~  ~override/%ee%.ITM~
  SAY NAME1 @110
  SAY NAME2 @110 SAY DESC @210
  WRITE_ASCII 0x3a ~%ii%~ #8    // icon
  WRITE_ASCII 0x58 ~CDAGG16~ #8    // description image
  LPF ALTER_ITEM_HEADER STR_VAR icon=~%ii%~ END




//non-Souls weapons

//Unbalanced Halberd (d2hal01)
OUTER_TEXT_SPRINT itemid  ~d2hal01~
INCLUDE ~%install%/variables.tph~
COPY ~%icons%/uhalberdi.BAM~          ~override/%ii%.BAM~
COPY ~%items%/uhalberd.ITM~           ~override/%itemid%.ITM~
  SAY NAME1 @111
  SAY NAME2 @111 SAY DESC @211
  WRITE_ASCII 0x3a ~%ii%~ #8       // icon
  WRITE_ASCII 0x58 ~CHALB01~ #8    // description image
  LPF ALTER_ITEM_HEADER STR_VAR icon=~%ii%~ END

COPY ~%effects%/castspelltarget.EFF~  ~override/%ee%.EFF~
  WRITE_ASCII 0x30 ~%ee%~
COPY ~%effects%/uhalberde.SPL~        ~override/%ee%.SPL~


//Club +4: Frenzy (d2ham01)
OUTER_TEXT_SPRINT itemid  ~d2ham01~
INCLUDE ~%install%/variables.tph~
COPY ~%icons%/clubfrenzyi.BAM~    ~override/%ii%.BAM~
COPY ~%items%/clubfrenzy.ITM~     ~override/%itemid%.ITM~
  SAY NAME1 @112
  SAY NAME2 @112 SAY DESC @212
  WRITE_ASCII 0x3a ~%ii%~ #8       // icon
  WRITE_ASCII 0x58 ~CBLUN01~ #8    // description image
  LPF ALTER_ITEM_HEADER STR_VAR icon=~%ii%~ END
  LPF ALTER_EFFECT INT_VAR header_type=1 match_opcode=139 parameter1=RESOLVE_STR_REF (@310) END    // "Confused"

COPY ~%effects%/clubfrenzys.SPL~  ~override/%ss%.SPL~    // main spell (for all below effects)
COPY ~%effects%/clubfrenzya.SPL~  ~override/%aa%.SPL~    // Enrage
  SAY NAME1 @307
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@309) END    // Berserk state message
  PATCH_IF (GAME_IS ~bgee iwdee~) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=267 END
    LPF d2_string_immunities END
  END
COPY ~%effects%/clubfrenzyc.SPL~  ~override/%cc%.SPL~    // winded

COPY ~%effects%/clubfrenzye.EFF~  ~override/%ee%.EFF~    // 0-15 probability
COPY ~%effects%/clubfrenzye.SPL~  ~override/%ee%.SPL~    // Frenzy
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@308) END    // "FRENZY!!!"
COPY ~%effects%/clubfrenzyb.SPL~  ~override/%bb%.SPL~    // Enrage (no text)
  PATCH_IF (GAME_IS ~bgee iwdee~) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=267 END
    LPF d2_string_immunities END
  END
COPY ~%effects%/clubfrenzyg.EFF~  ~override/%gg%.EFF~    // Color pulse

/*
Patch spells to block or remove Frenzy
Patch items to block Frenzy (not remove)
*/

ACTION_BASH_FOR ~override~  ~.*\.SPL~ BEGIN     // check override spells
  COPY_EXISTING ~%BASH_FOR_FILE%~  ~override~
    LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=4 opcode=321 parameter2=0 STR_VAR resource=~%ee%~ END
    LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=4 opcode=321 parameter2=0 STR_VAR resource=~%bb%~ END
    LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=3 opcode=318 parameter2=0 STR_VAR resource=~%ee%~ END
  BUT_ONLY
END

ACTION_FOR_EACH noberserk IN 
  ~ohtyr1~ 
  ~sppr321~ 
  ~#cureber~ 
BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%noberserk%.SPL~) BEGIN
    ACTION_IF !(FILE_EXISTS ~override/%noberserk%.SPL~) BEGIN    // skip if already patched
      COPY_EXISTING ~%noberserk%.SPL~  ~override~
        LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=4 opcode=321 parameter2=0 STR_VAR resource=~%ee%~ END
        LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=4 opcode=321 parameter2=0 STR_VAR resource=~%bb%~ END
        LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=3 opcode=318 parameter2=0 STR_VAR resource=~%ee%~ END
      BUT_ONLY
    END
  END
END

COPY_EXISTING_REGEXP GLOB ~.*\.ITM~  ~override~
  LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=3 opcode=318 parameter2=0 STR_VAR resource=~%ee%~ END
BUT_ONLY


//Scimitar +2: Accelerator (d2swd07)
OUTER_TEXT_SPRINT itemid  ~d2swd07~
INCLUDE ~%install%/variables.tph~
COPY ~%icons%/acceleratori.BAM~  ~override/%ii%.BAM~
COPY ~%items%/accelerator.ITM~   ~override/%itemid%.ITM~
  SAY NAME1 @113
  SAY NAME2 @113 SAY DESC @213
  WRITE_ASCII 0x3a ~%ii%~ #8        // icon
  WRITE_ASCII 0x58 ~CSW1H30~ #8    // description image
  LPF ALTER_ITEM_HEADER STR_VAR icon=~%ii%~ END

COPY ~%effects%/castspelltarget.EFF~  ~override/%ee%.EFF~
  WRITE_ASCII 0x30 ~%ee%~
COPY ~%effects%/acceleratore.SPL~     ~override/%ee%.SPL~
  

//Sling +2: Accelerator (d2eff01b)
OUTER_TEXT_SPRINT itemid  ~d2eff01b~
INCLUDE ~%install%/variables.tph~
COPY ~%icons%/slingacceli.BAM~  ~override/%itemid%.BAM~
COPY ~%items%/slingaccel.ITM~   ~override/%itemid%.ITM~
  SAY NAME1 @114
  SAY NAME2 @114 SAY DESC @214
  WRITE_ASCII 0x3a ~%ii%~ #8       // icon
  WRITE_ASCII 0x58 ~CSLNG07~ #8    // description image
  LPF ALTER_ITEM_HEADER STR_VAR icon=~%ii%~ END




//Set variables for external use
OUTER_TEXT_SPRINT blackbow     ~d2bow01~
OUTER_TEXT_SPRINT greatbow     ~d2bow02~
OUTER_TEXT_SPRINT whitebirch   ~d2bow03~
OUTER_TEXT_SPRINT avelyn       ~d2bow04~
OUTER_TEXT_SPRINT greatsword   ~d2swd01~
OUTER_TEXT_SPRINT fumeultraGS  ~d2swd02~
OUTER_TEXT_SPRINT chaosrapier  ~d2swd03~
OUTER_TEXT_SPRINT drakeblood   ~d2swd04~
OUTER_TEXT_SPRINT alonnesword  ~d2swd05~
OUTER_TEXT_SPRINT stoneGS      ~d2swd06~
OUTER_TEXT_SPRINT accelerator  ~d2swd07~
OUTER_TEXT_SPRINT aquamarine   ~d2dag01~
OUTER_TEXT_SPRINT uhalberd     ~d2hal01~
OUTER_TEXT_SPRINT clubfrenzy   ~d2ham01~
OUTER_TEXT_SPRINT slingaccel   ~d2eff01b~


/*
*/