/*
Additional patching of spells, items, or creatures.
Use for mass patching that attempts a CLONE_EFFECT on every file.
*/


OUTER_SET patch_spell = 0
OUTER_SET patch_item  = 0
OUTER_SET patch_cre   = 0


PRINT @6

//patch spells

COPY_EXISTING_REGEXP GLOB ~.*\.SPL~  ~override~

  PATCH_IF (SOURCE_SIZE > 0x99) BEGIN    // skip if no headers

    GET_OFFSET_ARRAY spl_headers  SPL_V10_HEADERS
    PHP_EACH spl_headers AS num => header_off BEGIN                    // array of headers
      GET_OFFSET_ARRAY2 spl_effects header_off SPL_V10_HEAD_EFFECTS
      PHP_EACH spl_effects AS num => effect_off BEGIN                  // array of effects on each header
        PATCH_IF (patch_spell = 0) BEGIN
          READ_SHORT effect_off opcode                                 // opcode
          PATCH_IF ((opcode = 101) OR (opcode = 4) OR     // 101 = block effect, 4 = cure berserk
                    (opcode = 163) OR (opcode = 16)       // 163 = free action, 16 = haste (skip haste 2)
          ) BEGIN
            SET patch_spell = 1
          END
        END
      END
    END

    PATCH_IF (patch_spell = 1) BEGIN
      LPF remove_frenzy END
      LPF remove_tranquil END
      LPF block_frenzy END
      LPF block_tranquil END
      SET patch_spell = 0
    END

  END
BUT_ONLY



//patch items

COPY_EXISTING_REGEXP GLOB ~.*\.ITM~  ~override~

  PATCH_IF (SOURCE_SIZE > 0xa0) BEGIN    // skip if no effects

    GET_OFFSET_ARRAY itm_equip  ITM_V10_GEN_EFFECTS
    PHP_EACH itm_equip AS num => equip_off BEGIN    // array of equipped effects
      PATCH_IF (patch_item = 0) BEGIN
        READ_SHORT equip_off opcode                 // opcode
        PATCH_IF (opcode = 101) BEGIN
          SET patch_item = 1
        END
      END
    END

    PATCH_IF (patch_item = 1) BEGIN
      LPF block_frenzy END
      LPF block_tranquil END
      SET patch_item = 0
    END

  END
BUT_ONLY



//patch creatures

COPY_EXISTING_REGEXP GLOB ~.*\.CRE~  ~override~

  PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN    // valid cre file
    READ_LONG 0x2c8 cre_effects
    PATCH_IF (cre_effects > 14) BEGIN    // immunities always have many effects
      SET patch_cre = 1
    END

    PATCH_IF (patch_cre = 1) BEGIN
      LPF block_frenzy END
      LPF block_tranquil END
      SET patch_cre = 0
    END
  END

BUT_ONLY


/*
*/