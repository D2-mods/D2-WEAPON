//v1.6+, updates/additions/etc.

//Club +4: Frenzy
COPY_EXISTING ~d2ham01e.SPL~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 126 parameter2 = 5 END
BUT_ONLY

//Stone Greatsword
ADD_PROJECTILE ~%MOD_FOLDER%/data2/d2swd06.PRO~
COPY_EXISTING ~d2swd06e.SPL~ ~override~
  WRITE_SHORT 0x98 ~%d2swd06%~
BUT_ONLY

//Sling +2: Accelerator
COPY_EXISTING ~d2eff01b.ITM~ ~override~
  SAY NAME2 @114 SAY DESC @214

  WRITE_LONG 0x1e (THIS BAND BNOT BIT23)    // racial usability flags
  WRITE_LONG 0x1e (THIS BAND BNOT BIT24)
  WRITE_LONG 0x1e (THIS BAND BNOT BIT25)
  WRITE_LONG 0x1e (THIS BAND BNOT BIT27)
  WRITE_LONG 0x1e (THIS BAND BNOT BIT28)
  WRITE_LONG 0x1e (THIS BAND BNOT BIT31)

  LPF DELETE_EFFECT INT_VAR match_opcode = 319 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 249 END

  LPF ADD_ITEM_EFFECT 
    INT_VAR 
      opcode = 177 
      header = 1
      type = 99    // can add to any header type
      target = 1 
      timing = 1 
      parameter1 = IDS_OF_SYMBOL (race ~halfling~)    // effect is for halflings only
      parameter2 = 4    // value from race.ids
    STR_VAR 
      resource = ~d2swd07e~    // use same effect as scimitar Accelerator
  END
BUT_ONLY

//add Bag of Holding with all weapons
ACTION_IF (GAME_IS ~bgee bg2ee eet~) BEGIN
  COPY ~%MOD_FOLDER%/data2/d2hbee1.ITM~ ~override/d2#wbag.ITM~
    SAY NAME1 @500 SAY UNIDENTIFIED_DESC @501
    SAY NAME2 @500
    WRITE_ASCII 0x3a ~iholding~ #8    // set inventory icon
	
  COPY ~%MOD_FOLDER%/data2/d2hbee1.STO~ ~override/d2#wbag.STO~
    SAY 0xc @500  /* Weapon Sack */
    LPF cannot_add_items END    // make it so items cannot be placed in the bag
END
  
ACTION_IF (GAME_IS ~iwdee~) BEGIN
  COPY ~%MOD_FOLDER%/data2/d2hbee0.ITM~ ~override/d2#wbag.ITM~
    SAY NAME1 @500
    SAY NAME2 @500 SAY DESC @501
    WRITE_ASCII 0x3a ~iholding~ #8    // set inventory icon
	
  COPY ~%MOD_FOLDER%/data2/d2hbee0.STO~ ~override/d2#wbag.STO~
    SAY 0xc @500  /* Weapon Sack */
    LPF cannot_add_items END    // make it so items cannot be placed in the bag
END

ACTION_IF !(FILE_EXISTS_IN_GAME ~IHOLDING.BAM~) BEGIN
  COPY ~%MOD_FOLDER%/data2/IHOLDING.BAM~ ~override~
END

INCLUDE ~%MOD_FOLDER%/install/wbag.tph~
  
  
  
/*


*/