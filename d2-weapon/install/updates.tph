//v1.6+, updates/additions/etc.


//Misc weapon updates

//Club +4: Frenzy
COPY_EXISTING ~d2ham01e.SPL~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 126 parameter2 = 5 END
BUT_ONLY

COPY_EXISTING ~d2ham01.ITM~ ~override~
  WRITE_ASCII 0x3a ~d2ham01i~ #8    // set inventory icon
  WRITE_ASCII 0x76 ~d2ham01i~ #8    // set quickbar icon
  WRITE_ASCII 0xae ~d2ham01i~ #8    // set ability icon
BUT_ONLY

//Unbalanced Halberd
COPY_EXISTING ~d2hal01.ITM~ ~override~
  WRITE_ASCII 0x3a ~d2hal01i~ #8    // set inventory icon
  WRITE_ASCII 0x76 ~d2hal01i~ #8    // set quickbar icon
BUT_ONLY

//Stone Greatsword
ADD_PROJECTILE ~%MOD_FOLDER%/data2/d2swd06.PRO~
COPY_EXISTING ~d2swd06e.SPL~ ~override~
  WRITE_SHORT 0x98 ~%d2swd06%~
BUT_ONLY

//Sling +2: Accelerator
COPY_EXISTING ~d2eff01b.ITM~ ~override~
  SAY NAME2 @114 SAY DESC @214

  WRITE_LONG 0x1e (THIS BAND BNOT BIT23)    // racial usability flags
  WRITE_LONG 0x1e (THIS BAND BNOT BIT24)
  WRITE_LONG 0x1e (THIS BAND BNOT BIT25)
  WRITE_LONG 0x1e (THIS BAND BNOT BIT27)
  WRITE_LONG 0x1e (THIS BAND BNOT BIT28)
  WRITE_LONG 0x1e (THIS BAND BNOT BIT31)

  LPF DELETE_EFFECT INT_VAR match_opcode = 319 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 249 END

  LPF ADD_ITEM_EFFECT 
    INT_VAR 
      opcode = 177 
      header = 1
      type = 99
      target = 1
      timing = 1
      parameter1 = IDS_OF_SYMBOL (race ~halfling~)    // effect is for halflings only
      parameter2 = 4    // use race.ids for parameter1
    STR_VAR
      resource = ~d2swd07e~    // use same effect as scimitar Accelerator
  END
BUT_ONLY



//add Bag of Holding with all weapons

ACTION_IF (GAME_IS ~bgee bg2ee eet~) BEGIN
  COPY ~%MOD_FOLDER%/data2/d2hbee1.ITM~ ~override/d2#wbag.ITM~
    SAY NAME1 @500 SAY UNIDENTIFIED_DESC @501
    SAY NAME2 @500
    WRITE_ASCII 0x3a ~d2#wbag~ #8    // set inventory icon
	
  COPY ~%MOD_FOLDER%/data2/d2hbee1.STO~ ~override/d2#wbag.STO~
    SAY 0xc @500  /* Weapon Sack */
    LPF cannot_add_items END    // make it so items cannot be placed in the bag
END
  
ACTION_IF (GAME_IS ~iwdee~) BEGIN
  COPY ~%MOD_FOLDER%/data2/d2hbee0.ITM~ ~override/d2#wbag.ITM~
    SAY NAME1 @500
    SAY NAME2 @500 SAY DESC @501
    WRITE_ASCII 0x3a ~d2#wbag~ #8    // set inventory icon
	
  COPY ~%MOD_FOLDER%/data2/d2hbee0.STO~ ~override/d2#wbag.STO~
    SAY 0xc @500  /* Weapon Sack */
    LPF cannot_add_items END    // make it so items cannot be placed in the bag
END

COPY ~%MOD_FOLDER%/data2/d2#wbag.BAM~ ~override~
INCLUDE ~%MOD_FOLDER%/install/wbag.tph~
  
  
  
//Add damage to bows for IWDEE
ACTION_IF (GAME_IS ~iwdee~) BEGIN

  COPY_EXISTING ~d2bow01.ITM~ ~override~  
    WRITE_SHORT 0x86 5    // +5 THAC0 (was +7)
    WRITE_SHORT 0x8c 2    // +2 damage (was +0)
    SAY DESC @900
  BUT_ONLY
  
  COPY_EXISTING ~d2bow03.ITM~ ~override~
    WRITE_SHORT 0x8c 3    // +3 damage (was +0)
    SAY DESC @902
  BUT_ONLY
  
  COPY_EXISTING ~d2bow04.ITM~ ~override~
    WRITE_SHORT 0x8c 1    // +1 damage (was +0)
    SAY DESC @903
  BUT_ONLY
  
END



//Standardize prices of items
ACTION_DEFINE_ASSOCIATIVE_ARRAY d2setprices BEGIN
  d2hal01 => 1
  d2bow03 => 8000
  d2swd01 => 8000
  d2swd07 => 8000
  d2ham01 => 8000
  d2eff01b => 8000
  d2bow01 => 15000
  d2bow02 => 15000
  d2swd04 => 15000
  d2swd06 => 15000
  d2dag01 => 15000
  d2bow04 => 22000
  d2swd02 => 22000
  d2swd03 => 22000
  d2swd05 => 22000
END

ACTION_PHP_EACH d2setprices AS item => price BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%item%.itm~ BEGIN
    COPY_EXISTING ~%item%.itm~ ~override~
      PATCH_IF SOURCE_SIZE > 0x71 BEGIN
        WRITE_LONG 0x34 price
      END
    BUT_ONLY
  END
END




/*


*/